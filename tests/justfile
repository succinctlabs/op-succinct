SOURCE := source_directory()
ROOT := parent_directory(SOURCE)

test-e2e-sysgo PKG="" FILTER="" TIMEOUT="90m": build-contracts unzip-contract-artifacts
    #!/bin/bash

    if ! [ -z "{{FILTER}}" ]; then
        export FILTER="-run {{FILTER}}"
    fi

    export DEVSTACK_ORCHESTRATOR=sysgo
    export OP_DEPLOYER_ARTIFACTS="{{SOURCE}}/artifacts/src/forge-artifacts"
    export DISABLE_OP_E2E_LEGACY=true

    # Metrics off by default. Enable with: SYSGO_METRICS_ENABLED=true just test-e2e-sysgo ...
    export SYSGO_METRICS_ENABLED="${SYSGO_METRICS_ENABLED:-false}"
    if [ "$SYSGO_METRICS_ENABLED" = "true" ]; then
        export SYSGO_GRAFANA_PROVISIONING_DIR="{{SOURCE}}/monitoring/grafana"
    fi

    TEST_PATH="{{PKG}}"
    BUILD_ARGS=""

    if [ -z "$TEST_PATH" ]; then
        TEST_PATH="./e2e/..."
        BUILD_ARGS="--bin validity --bin proposer --bin challenger"
        export VALIDITY_PROPOSER_EXEC_PATH="{{ROOT}}/target/release/validity"
        export FAULT_PROOF_PROPOSER_EXEC_PATH="{{ROOT}}/target/release/proposer"
        export FAULT_PROOF_CHALLENGER_EXEC_PATH="{{ROOT}}/target/release/challenger"
    elif [[ "$TEST_PATH" == *"validity"* ]]; then
        BUILD_ARGS="--bin validity"
        export VALIDITY_PROPOSER_EXEC_PATH="{{ROOT}}/target/release/validity"
    elif [[ "$TEST_PATH" == *"faultproof"* ]]; then
        BUILD_ARGS="--bin proposer --bin challenger"
        export FAULT_PROOF_PROPOSER_EXEC_PATH="{{ROOT}}/target/release/proposer"
        export FAULT_PROOF_CHALLENGER_EXEC_PATH="{{ROOT}}/target/release/challenger"
    else
        echo "PKG must contain 'validity' or 'faultproof' to choose a binary."
        exit 1
    fi

    echo "Building binaries: $BUILD_ARGS ..."
    cd .. && cargo build --release $BUILD_ARGS

    # Run the test with count=1 to avoid caching the test results.
    echo "Running go test $TEST_PATH $FILTER"
    cd {{SOURCE}} && go test -p 1 -count=1 -timeout {{TIMEOUT}} -v "$TEST_PATH" $FILTER

# AltDA e2e tests - builds validity binary with altda feature
test-e2e-sysgo-altda FILTER="" TIMEOUT="90m": build-contracts unzip-contract-artifacts
    #!/bin/bash

    if ! [ -z "{{FILTER}}" ]; then
        export FILTER="-run {{FILTER}}"
    fi

    export DEVSTACK_ORCHESTRATOR=sysgo
    export OP_DEPLOYER_ARTIFACTS="{{SOURCE}}/artifacts/src/forge-artifacts"
    export DISABLE_OP_E2E_LEGACY=true

    export SYSGO_METRICS_ENABLED="${SYSGO_METRICS_ENABLED:-false}"
    if [ "$SYSGO_METRICS_ENABLED" = "true" ]; then
        export SYSGO_GRAFANA_PROVISIONING_DIR="{{SOURCE}}/monitoring/grafana"
    fi

    echo "Building validity binary with altda feature..."
    cd .. && cargo build --release --bin validity --features altda --no-default-features
    export VALIDITY_PROPOSER_EXEC_PATH="{{ROOT}}/target/release/validity"

    echo "Running AltDA e2e tests $FILTER"
    cd {{SOURCE}} && go test -p 1 -count=1 -timeout {{TIMEOUT}} -v "./e2e/validity/altda/..." $FILTER

# Long-running tests - logs to tests/logs/<mode>-<timestamp>.log
# Usage: just long-running <nodes|validity|faultproof|faultproof-ff>
long-running MODE:
    #!/bin/bash
    set -euo pipefail
    case "{{MODE}}" in
        nodes)         just logged {{MODE}} just nodes ;;
        validity)      just logged {{MODE}} just test-e2e-sysgo "./e2e/validity/long-running/..." "TestValidityProposer_LongRunning" "0" ;;
        faultproof)    just logged {{MODE}} just test-e2e-sysgo "./e2e/faultproof/long-running/..." "TestFaultProofProposer_LongRunning" "0" ;;
        faultproof-ff) just logged {{MODE}} just test-e2e-sysgo "./e2e/faultproof/long-running/..." "TestFaultProofProposer_FastFinality_LongRunning" "0" ;;
        *) echo "Usage: just long-running <nodes|validity|faultproof|faultproof-ff>"; exit 1 ;;
    esac

# Run only L1/L2 nodes (no proposer) - outputs RPC endpoints to tests/.env
nodes: build-contracts unzip-contract-artifacts
    #!/bin/bash
    export DEVSTACK_ORCHESTRATOR=sysgo
    export OP_DEPLOYER_ARTIFACTS="{{SOURCE}}/artifacts/src/forge-artifacts"
    export DISABLE_OP_E2E_LEGACY=true
    cd {{SOURCE}} && go test -p 1 -count=1 -timeout 0 -v "./e2e/nodes/..." -run TestNodes_RunOnly

unzip-contract-artifacts DEST_DIR="":
    #!/bin/bash
    set -euo pipefail

    DEST_DIR="{{DEST_DIR}}"
    if [ -z "$DEST_DIR" ]; then
        DEST_DIR="{{SOURCE}}/artifacts/src"
    fi

    ARCHIVE="{{SOURCE}}/artifacts/compressed/artifacts.tzst"
    if [ ! -f "$ARCHIVE" ]; then
        echo "Missing artifact at $ARCHIVE. Run 'just update-packages' first." >&2
        exit 1
    fi

    mkdir -p "$DEST_DIR"
    zstd -d -c "$ARCHIVE" | tar -x -C "$DEST_DIR"

update-packages: submodule
    #!/bin/bash
    cd {{SOURCE}}/optimism/packages/contracts-bedrock && forge build --skip "/**/test/**" -D never
    cd {{SOURCE}}/optimism/op-deployer && just copy-contract-artifacts
    cp {{SOURCE}}/optimism/op-deployer/pkg/deployer/artifacts/forge-artifacts/artifacts.tzst {{SOURCE}}/artifacts/compressed/artifacts.tzst

submodule:
    git submodule update --init --recursive

# Build op-succinct contracts and verifier scripts
build-contracts: submodule
    #!/bin/bash
    set -euo pipefail
    echo "Building contracts..."
    cd {{ROOT}}/contracts && forge build
    echo "Building verifier scripts..."
    cd {{SOURCE}} && forge build

# Logging wrapper - saves output to tests/logs/<name>-<timestamp>.log
logged NAME +CMD:
    #!/bin/bash
    set -euo pipefail
    LOG_DIR="{{SOURCE}}/logs"
    mkdir -p "$LOG_DIR"
    LOG_FILE="$LOG_DIR/{{NAME}}-$(date +%Y%m%d-%H%M%S).log"
    echo "Logging to: $LOG_FILE"
    exec > >(tee "$LOG_FILE") 2>&1
    {{CMD}}
