SOURCE := source_directory()
ROOT := parent_directory(SOURCE)

test-e2e-sysgo PKG="" FILTER="" TIMEOUT="90m": unzip-contract-artifacts
    #!/bin/bash

    if ! [ -z "{{FILTER}}" ]; then
        export FILTER="-run {{FILTER}}"
    fi

    export DEVSTACK_ORCHESTRATOR=sysgo
    export OP_DEPLOYER_ARTIFACTS="{{SOURCE}}/artifacts/src/forge-artifacts"
    export DISABLE_OP_E2E_LEGACY=true

    # Metrics off by default. Enable with: SYSGO_METRICS_ENABLED=true just test-e2e-sysgo ...
    export SYSGO_METRICS_ENABLED="${SYSGO_METRICS_ENABLED:-false}"
    if [ "$SYSGO_METRICS_ENABLED" = "true" ]; then
        export SYSGO_GRAFANA_PROVISIONING_DIR="{{SOURCE}}/monitoring"
    fi

    TEST_PATH="{{PKG}}"
    BUILD_ARGS=""

    if [ -z "$TEST_PATH" ]; then
        TEST_PATH="./e2e/..."
        BUILD_ARGS="--bin validity --bin proposer"
        export VALIDITY_PROPOSER_EXEC_PATH="{{ROOT}}/target/release/validity"
        export FAULT_PROOF_PROPOSER_EXEC_PATH="{{ROOT}}/target/release/proposer"
    elif [[ "$TEST_PATH" == *"validity"* ]]; then
        BUILD_ARGS="--bin validity"
        export VALIDITY_PROPOSER_EXEC_PATH="{{ROOT}}/target/release/validity"
    elif [[ "$TEST_PATH" == *"faultproof"* ]]; then
        BUILD_ARGS="--bin proposer"
        export FAULT_PROOF_PROPOSER_EXEC_PATH="{{ROOT}}/target/release/proposer"
    else
        echo "PKG must contain 'validity' or 'faultproof' to choose a binary."
        exit 1
    fi

    echo "Building binaries: $BUILD_ARGS ..."
    cd .. && cargo build --release $BUILD_ARGS

    # Run the test with count=1 to avoid caching the test results.
    echo "Running go test $TEST_PATH $FILTER"
    cd {{SOURCE}} && go test -p 1 -count=1 -timeout {{TIMEOUT}} -v "$TEST_PATH" $FILTER

# Long-running tests - keep the stack running for manual debugging (no failure on lag)
# Usage: just long-running validity OR just long-running faultproof
long-running MODE:
    #!/bin/bash
    set -euo pipefail
    MODE="{{MODE}}"
    if [[ "$MODE" == "validity" ]]; then
        just test-e2e-sysgo "./e2e/validity/long-running/..." "TestValidityProposer_LongRunning" "0"
    elif [[ "$MODE" == "faultproof" ]]; then
        just test-e2e-sysgo "./e2e/faultproof/long-running/..." "TestFaultProofProposer_LongRunning" "0"
    else
        echo "Usage: just long-running <validity|faultproof>"
        exit 1
    fi

unzip-contract-artifacts DEST_DIR="":
    #!/bin/bash
    set -euo pipefail

    DEST_DIR="{{DEST_DIR}}"
    if [ -z "$DEST_DIR" ]; then
        DEST_DIR="{{SOURCE}}/artifacts/src"
    fi

    ARCHIVE="{{SOURCE}}/artifacts/compressed/artifacts.tzst"
    if [ ! -f "$ARCHIVE" ]; then
        echo "Missing artifact at $ARCHIVE. Run 'just update-packages' first." >&2
        exit 1
    fi

    mkdir -p "$DEST_DIR"
    zstd -d -c "$ARCHIVE" | tar -x -C "$DEST_DIR"

update-packages: submodule
    #!/bin/bash
    cd {{SOURCE}}/optimism/op-deployer && just build-contracts copy-contract-artifacts
    cp {{SOURCE}}/optimism/op-deployer/pkg/deployer/artifacts/forge-artifacts/artifacts.tzst {{SOURCE}}/artifacts/compressed/artifacts.tzst

submodule:
    git submodule update --init --recursive
